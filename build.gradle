import io.papermc.hangarpublishplugin.HangarPublishTask

plugins {
    alias(libs.plugins.fabric.loom) apply false
    alias(libs.plugins.neoforge.moddev) apply false
    alias(libs.plugins.hangar)
}


def changelogMd = file('CHANGELOG.md')
ext {
    changelog = changelogMd.exists() ? changelogMd.text : ''
    minecraft_version_list = minecraft_versions.split(",").toList()
}

def copyShadowJar = it -> {
    it.include('*-all.jar', '*-javadoc.jar', '*-sources.jar')
    it.rename { String fileName ->
        fileName.replace('-all', '')
    }
}

tasks.register('buildAll', Copy) {
    dependsOn(':bukkit:shadowJar', ':velocity:shadowJar', ':bungeecord:shadowJar',
            ':forge:jarJar', ':fabric:build', ':neoforge:build')

    from('fabric/build/libs', 'neoforge/build/libs')
    from('bukkit/build/libs', copyShadowJar)
    from('bungeecord/build/libs', copyShadowJar)
    from('velocity/build/libs', copyShadowJar)
    from('forge/build/libs', copyShadowJar)

    it.rename { String fileName ->
        fileName.replace(mod_name, mod_id)
    }

    into('result')
}

tasks.register('clean', Delete) {
    delete 'result'
}

hangarPublish {
    evaluationDependsOn(":bukkit")
    evaluationDependsOn(":bungeecord")
    evaluationDependsOn(":velocity")

    publications.register('plugin') {
        version = project.version
        id = 'hangar-project'
        channel = 'Release'
        changelog = rootProject.changelog

        apiKey = findProperty('hangar_api_key') as String ?: ''

        // register platforms
        platforms {
            paper {
                jar = project(":bukkit").tasks.named("shadowJar").get().outputs.files.singleFile
                platformVersions = minecraft_version_list
            }
            waterfall {
                jar = project(":bungeecord").tasks.named("shadowJar").get().outputs.files.singleFile
                platformVersions = minecraft_version_list
            }
            velocity {
                jar = project(":velocity").tasks.named("shadowJar").get().outputs.files.singleFile
                platformVersions = minecraft_version_list
            }
        }
    }
}

tasks.withType(HangarPublishTask).configureEach {
    dependsOn(':bukkit:shadowJar', ':velocity:shadowJar', ':bungeecord:shadowJar')
}

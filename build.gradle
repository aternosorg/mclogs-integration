import io.papermc.hangarpublishplugin.HangarPublishTask

plugins {
    alias(libs.plugins.fabric.loom) apply false
    alias(libs.plugins.neoforge.moddev) apply false
    alias(libs.plugins.hangar)
}

String getChangeLog() {
    def input = file('CHANGELOG.md').text.readLines()
    def output = []
    def foundHeader = false
    for (line in input) {
        if (!foundHeader && line.startsWith('# ')) {
            foundHeader = true
            continue
        }
        if (foundHeader) {
            if (line.startsWith('---')) break
            output << line
        }
    }
    return output.join('\n').trim()
}

tasks.register('generateReleaseBody') {
    doLast {
        file('release-body.md').text = getChangeLog()
    }
}

ext {
    changelog = getChangeLog()
    minecraft_version_list = minecraft_versions.split(",").toList()
    velocity_version_list = velocity_versions.split(",").toList()
}

def copyShadowJar = it -> {
    it.include('*-all.jar', '*-javadoc.jar', '*-sources.jar')
    it.rename { String fileName ->
        fileName.replace('-all', '')
    }
}

tasks.register('buildAll', Copy) {
    dependsOn(':forge:jarJar', ':fabric:build', ':neoforge:build')

    from('fabric/build/libs', 'neoforge/build/libs')
    from('forge/build/libs', copyShadowJar)

    it.rename { String fileName ->
        fileName.replace(mod_name, mod_id)
    }

    into('result')
}

tasks.register('clean', Delete) {
    delete 'result'
}

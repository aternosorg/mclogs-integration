import net.darkhax.curseforgegradle.TaskPublishCurseForge

plugins {
    id 'multiloader-loader'
    alias(libs.plugins.neoforge.moddev)
    alias(libs.plugins.minotaur)
    alias(libs.plugins.curseforge.gradle)
}

neoForge {
    version = neoforge_version
    // Automatically enable neoforge AccessTransformers if the file exists
    def at = project(':common-mc').file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.add(at.absolutePath)
    }
    if (enable_parchment) {
        parchment {
            minecraftVersion = parchment_minecraft
            mappingsVersion = parchment_version
        }
    }
    runs {
        configureEach {
            systemProperty('neoforge.enabledGameTestNamespaces', mod_id)
            ideName = "NeoForge ${it.name.capitalize()} (${project.path})" // Unify the run config names with fabric
        }
        client {
            gameDirectory = file('runs/client')
            client()
        }
        server {
            gameDirectory = file('runs/server')
            server()
        }
    }
    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}


dependencies {
    implementation(libs.mclogs.api) {
        exclude group: 'com.google.code.gson', module: 'gson'
    }
    implementation(libs.mclogs.api) {
        exclude group: 'com.google.code.gson', module: 'gson'
    }
    jarJar(libs.mclogs.api)
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

modrinth {
    token = findProperty('modrinth_api_token') as String ?: ''
    projectId = modrinth_project_id
    uploadFile = jar
    gameVersions = minecraft_version_list
    loaders = ["neoforge"]
    changelog = rootProject.changelog
}

tasks.register('curseforge', TaskPublishCurseForge) {
    apiToken = findProperty('curseforge_token') as String ?: ''
    def mainFile = upload(curseforge_project_id_mod, jar)
    mainFile.releaseType = "release"
    mainFile.changelog = changelog
    mainFile.changelogType = "markdown"
    mainFile.addModLoader("NeoForge")

    for (String version in minecraft_version_list) {
        mainFile.addGameVersion(version)
    }
}
